plugins {
    id "io.spring.dependency-management" version "1.0.5.RELEASE"
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: "io.spring.dependency-management"

    project.buildDir = file("${project.parent.buildDir}/${project.name}")

    repositories {
        jcenter()
    }

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_10
        targetCompatibility = JavaVersion.VERSION_1_10

        doFirst {
            options.compilerArgs = [
                '--module-path', classpath.asPath,
            ]
            classpath = files()
        }
    }

    sourceSets {
        main {
            java {
                srcDirs = ['main/java']
            }
            resources {
                srcDirs = ['main/resources']
            }
        }
        test {
            java {
                srcDirs = ['test/java']
            }
            resources {
                srcDirs = ['test/resources']
            }
        }
    }

    idea {
        module {
            def baseOutDir = "${project.parent.projectDir}/out/${project.name}"
            outputDir file("${baseOutDir}/classes/main")
            testOutputDir file("${baseOutDir}/classes/test")
        }
    }

}
