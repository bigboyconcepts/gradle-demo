plugins {
    id "io.spring.dependency-management" version "1.0.5.RELEASE"
}

ext {
    javaVersion = JavaVersion.VERSION_1_10
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: "io.spring.dependency-management"

    group = 'app'
    version = '1.0.0-SNAPSHOT'

    repositories {
        jcenter()
    }

    compileJava {
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        buildDir = file("${project.parent.buildDir}/${project.name}")
        doFirst {
            if (javaVersion.isJava9Compatible()) {
                options.compilerArgs = [
                    '--module-path', classpath.asPath,
                ]
                classpath = files()
            }
        }
    }

    sourceSets {
        main {
            java {
                srcDirs = ['main/java']
                if (!javaVersion.isJava9Compatible()) {
                    exclude 'module-info.java'
                }
            }
            resources {
                srcDirs = ['main/resources']
            }
        }
        test {
            java {
                srcDirs = ['test/java']
            }
            resources {
                srcDirs = ['test/resources']
            }
        }
    }

    idea {
        module {
            def baseOutDir = "${parent.projectDir}/out/${name}"
            outputDir file("${baseOutDir}/classes/main")
            testOutputDir file("${baseOutDir}/classes/test")
        }
    }

}
